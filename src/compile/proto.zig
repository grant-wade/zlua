const std = @import("std");
const bytecode = @import("bytecode.zig");

pub const LineInfo = struct {
    line: usize,
};

pub const LocalDebug = struct {
    name: []const u8,
    register: bytecode.Register,
    start_pc: usize,
    end_pc: usize = 0,
    to_close: bool = false,
};

pub const UpvalueDesc = struct {
    name: []const u8,
    in_stack: bool,
    index: u16,
};

pub const ErrorOp = enum {
    arithmetic,
    bitwise,
    concat,
    compare,
    call,
    index,
    newindex,
    length,
    numeric_for,
};

pub const OperandOrigin = union(enum) {
    temporary,
    local: []const u8,
    upvalue: []const u8,
    global: []const u8,
    field: []const u8,
    method: []const u8,
    metamethod: []const u8,
    constant: []const u8,
};

pub const ErrorSite = struct {
    line: usize,
    op: ErrorOp,
    operands: []const OperandOrigin = &.{},
    call_name: ?OperandOrigin = null,
};

pub const ErrorSiteEntry = struct {
    pc: usize,
    site: ErrorSite,
};

pub const Proto = struct {
    allocator: std.mem.Allocator,
    arena: std.heap.ArenaAllocator,
    constants: std.ArrayList(bytecode.Constant) = .empty,
    instructions: std.ArrayList(bytecode.Instruction) = .empty,
    line_info: std.ArrayList(LineInfo) = .empty,
    locals: std.ArrayList(LocalDebug) = .empty,
    upvalues: std.ArrayList(UpvalueDesc) = .empty,
    error_sites: std.ArrayList(ErrorSiteEntry) = .empty,
    children: std.ArrayList(*Proto) = .empty,
    max_registers: u16 = 0,
    param_count: u16 = 0,
    is_vararg: bool = false,
    named_vararg: bool = false,
    source_name: []const u8 = "zlua",
    debug_name: ?[]const u8 = null,
    defined_line: usize = 0,
    last_defined_line: usize = 0,
    has_to_close_locals: bool = false,

    pub fn init(allocator: std.mem.Allocator) Proto {
        return .{
            .allocator = allocator,
            .arena = std.heap.ArenaAllocator.init(allocator),
        };
    }

    pub fn deinit(self: *Proto) void {
        for (self.children.items) |child| {
            child.deinit();
            self.allocator.destroy(child);
        }
        self.children.deinit(self.allocator);
        self.error_sites.deinit(self.allocator);
        self.upvalues.deinit(self.allocator);
        self.locals.deinit(self.allocator);
        self.line_info.deinit(self.allocator);
        self.instructions.deinit(self.allocator);
        self.constants.deinit(self.allocator);
        self.arena.deinit();
        self.* = undefined;
    }

    pub fn pc(self: Proto) usize {
        return self.instructions.items.len;
    }

    /// Direct reads of an unmodified named vararg can use the argument slice.
    /// Any escape, write, or capture requires the ordinary table representation.
    pub fn hasReadOnlyNamedVararg(self: *const Proto) bool {
        if (!self.named_vararg) return false;
        const register = self.param_count;
        for (self.children.items) |child| {
            for (child.upvalues.items) |upvalue| {
                if (upvalue.in_stack and upvalue.index == register) return false;
            }
        }
        for (self.instructions.items) |instruction| {
            const touches = switch (instruction) {
                .get_table => |op| op.dest == register or op.key == register,
                .get_field => |op| op.dest == register,
                .jmp, .close => false,
                .load_nil, .check_close, .close_tbc => |r| r == register,
                inline .load_bool, .load_const, .new_table, .closure => |op| op.dest == register,
                inline .move, .unm, .bnot, .not, .len => |op| op.dest == register or op.source == register,
                inline .get_global, .set_global, .get_upvalue, .set_upvalue, .test_op => |op| op.register == register,
                .declare_global => |op| op.table == register or op.value == register,
                .set_table => |op| op.table == register or op.key == register or op.value == register,
                inline .set_array, .set_field => |op| op.table == register or op.value == register,
                .set_list => |op| op.table == register or op.first <= register,
                .add, .sub, .mul, .div, .idiv, .mod, .pow, .band, .bor, .bxor, .shl, .shr, .eq, .lt, .le, .concat => |op| op.dest == register or op.left == register or op.right == register,
                .compare_branch => |op| op.left == register or op.right == register,
                .test_set => |op| op.dest == register or op.source == register,
                // Conservatively reject ranges that could include the binding.
                inline .call, .tail_call, .for_prep, .for_loop, .tfor_prep, .tfor_call, .tfor_loop => |op| op.base <= register,
                .ret => |op| op.first <= register,
                .vararg => |op| op.dest <= register,
            };
            if (touches) return false;
        }
        return true;
    }

    pub fn emit(self: *Proto, instruction: bytecode.Instruction, line: usize) !usize {
        const index = self.instructions.items.len;
        try self.instructions.append(self.allocator, instruction);
        try self.line_info.append(self.allocator, .{ .line = line });
        return index;
    }

    pub fn patchJump(self: *Proto, index: usize, target_pc: usize) !void {
        const offset = try jumpOffset(index, target_pc);
        switch (self.instructions.items[index]) {
            .jmp => self.instructions.items[index].jmp = offset,
            .compare_branch => self.instructions.items[index].compare_branch.offset = offset,
            .test_op => self.instructions.items[index].test_op.offset = offset,
            .test_set => self.instructions.items[index].test_set.offset = offset,
            .for_prep => self.instructions.items[index].for_prep.offset = offset,
            .for_loop => self.instructions.items[index].for_loop.offset = offset,
            .tfor_prep => self.instructions.items[index].tfor_prep.offset = offset,
            .tfor_loop => self.instructions.items[index].tfor_loop.offset = offset,
            else => return error.InvalidJumpPatch,
        }
    }

    pub fn addConstant(self: *Proto, constant: bytecode.Constant) !bytecode.ConstantIndex {
        const owned = try self.ownConstant(constant);
        for (self.constants.items, 0..) |existing, index| {
            if (bytecode.constantEql(existing, owned)) return @intCast(index);
        }
        try self.constants.append(self.allocator, owned);
        return @intCast(self.constants.items.len - 1);
    }

    pub fn addLocal(self: *Proto, local: LocalDebug) !usize {
        if (local.to_close) self.has_to_close_locals = true;
        try self.locals.append(self.allocator, .{
            .name = try self.dupe(local.name),
            .register = local.register,
            .start_pc = local.start_pc,
            .end_pc = local.end_pc,
            .to_close = local.to_close,
        });
        return self.locals.items.len - 1;
    }

    pub fn addUpvalue(self: *Proto, upvalue: UpvalueDesc) !bytecode.UpvalueIndex {
        for (self.upvalues.items, 0..) |existing, index| {
            if (existing.in_stack == upvalue.in_stack and existing.index == upvalue.index and std.mem.eql(u8, existing.name, upvalue.name)) {
                return @intCast(index);
            }
        }
        try self.upvalues.append(self.allocator, .{
            .name = try self.dupe(upvalue.name),
            .in_stack = upvalue.in_stack,
            .index = upvalue.index,
        });
        return @intCast(self.upvalues.items.len - 1);
    }

    pub fn addChild(self: *Proto, child: *Proto) !bytecode.ProtoIndex {
        try self.children.append(self.allocator, child);
        return @intCast(self.children.items.len - 1);
    }

    pub fn addErrorSite(self: *Proto, pc_index: usize, site: ErrorSite) !void {
        const operands = try self.arena.allocator().alloc(OperandOrigin, site.operands.len);
        for (site.operands, 0..) |origin, index| operands[index] = try self.ownOrigin(origin);
        try self.error_sites.append(self.allocator, .{
            .pc = pc_index,
            .site = .{
                .line = site.line,
                .op = site.op,
                .operands = operands,
                .call_name = if (site.call_name) |origin| try self.ownOrigin(origin) else null,
            },
        });
    }

    pub fn errorSiteAt(self: Proto, pc_index: usize) ?ErrorSite {
        for (self.error_sites.items) |entry| {
            if (entry.pc == pc_index) return entry.site;
            if (entry.pc > pc_index) return null;
        }
        return null;
    }

    pub fn setDebugName(self: *Proto, name: []const u8) !void {
        self.debug_name = try self.dupe(name);
    }

    fn ownConstant(self: *Proto, constant: bytecode.Constant) !bytecode.Constant {
        return switch (constant) {
            .nil => .nil,
            .boolean => |value| .{ .boolean = value },
            .integer => |value| .{ .integer = try self.dupe(value) },
            .number => |value| .{ .number = try self.dupe(value) },
            .string => |value| .{ .string = try self.dupe(value) },
        };
    }

    fn ownOrigin(self: *Proto, origin: OperandOrigin) !OperandOrigin {
        return switch (origin) {
            .temporary => .temporary,
            .local => |name| .{ .local = try self.dupe(name) },
            .upvalue => |name| .{ .upvalue = try self.dupe(name) },
            .global => |name| .{ .global = try self.dupe(name) },
            .field => |name| .{ .field = try self.dupe(name) },
            .method => |name| .{ .method = try self.dupe(name) },
            .metamethod => |name| .{ .metamethod = try self.dupe(name) },
            .constant => |name| .{ .constant = try self.dupe(name) },
        };
    }

    fn dupe(self: *Proto, value: []const u8) ![]const u8 {
        return self.arena.allocator().dupe(u8, value);
    }
};

fn jumpOffset(index: usize, target_pc: usize) !bytecode.JumpOffset {
    const from: isize = @intCast(index + 1);
    const to: isize = @intCast(target_pc);
    return std.math.cast(bytecode.JumpOffset, to - from) orelse error.JumpOutOfRange;
}
